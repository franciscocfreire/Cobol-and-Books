000010***************************************************************** 
000020 IDENTIFICATION DIVISION.                                         
000030***************************************************************** 
000040                                                                  
000050 PROGRAM-ID. CLLP7910.                                            
000060*===============================================================* 
000070*                   C P M   S I S T E M A S                     * 
000080*---------------------------------------------------------------* 
000090*                                                               * 
000100*      PROGRAMA     : CLLP7910                                  * 
000110*      PROGRAMADOR  : RONALD                                    * 
000120*      ANALISTA     : HOMERO TOZETTO  GRP = 82                  * 
000130*      DATA         : 28/11/1997                                * 
000140*                                                               * 
000150*      OBJETIVO     : GRAVAR NUMERO E DIGITO DE CL              * 
000160*                                                               * 
000170*---------------------------------------------------------------* 
000180*                                                               * 
000190*      ARQUIVOS:                                                * 
000200*                                                               * 
000210*         DDNAME                           INCLUDE/BOOK         * 
000220*                                                               * 
000230*         MOVTO    (INPUT)  - LRECL = 675    I#TTLPA9           *
000240*                                                               * 
000250*         MOVOK    (OUTPUT) - LRECL = 675                       *
000260*                                                               * 
000270*===============================================================* 
000280     EJECT                                                        
000290***************************************************************** 
000300 ENVIRONMENT DIVISION.                                            
000310***************************************************************** 
000320*---------------------------------------------------------------* 
000330 CONFIGURATION SECTION.                                           
000340*---------------------------------------------------------------* 
000350                                                                  
000360 SPECIAL-NAMES.                                                   
000370     DECIMAL-POINT IS COMMA.                                      
000380                                                                  
000390*---------------------------------------------------------------* 
000400 INPUT-OUTPUT SECTION.                                            
000410*---------------------------------------------------------------* 
000420                                                                  
000430 FILE-CONTROL.                                                    
000440                                                                  
000450     SELECT  MOVTO        ASSIGN    TO    UT-S-MOVTO              
000460             FILE         STATUS    IS    WRK-FS-MOVTO.           
000470                                                                  
000480     SELECT  MOVOK        ASSIGN    TO    UT-S-MOVOK              
000490             FILE         STATUS    IS    WRK-FS-MOVOK.           
000500                                                                  
000510***************************************************************** 
000520 DATA DIVISION.                                                   
000530***************************************************************** 
000540*---------------------------------------------------------------* 
000550 FILE SECTION.                                                    
000560*---------------------------------------------------------------* 
000570                                                                  
000580*---------------------------------------------------------------* 
000590*    INPUT:     ORG. SEQUENCIAL   -   LRECL = 675               *
000600*---------------------------------------------------------------* 
000610                                                                  
000620 FD  MOVTO                                                        
000630     RECORDING MODE IS F                                          
000640     LABEL RECORD IS STANDARD                                     
000650     BLOCK CONTAINS 0 RECORDS.                                    
000660                                                                  
       COPY 'I#CLLPRT'.
000680                                                                  
000690*---------------------------------------------------------------* 
000700*    OUTPUT:    ORG. SEQUENCIAL   -   LRECL = 675               *
000710*---------------------------------------------------------------* 
000720                                                                  
000730 FD  MOVOK                                                        
000740     RECORDING MODE IS F                                          
000750     LABEL RECORD IS STANDARD                                     
000760     BLOCK CONTAINS 0 RECORDS.                                    
000770                                                                  
000780 01  REG-MOVOK                    PIC X(675).
000790                                                                  
000800***************************************************************** 
000810 WORKING-STORAGE SECTION.                                         
000820***************************************************************** 
000830 77  FILLER                      PIC X(32)        VALUE           
000840     '* INICIO DA WORKING *'.                                     
000850                                                                  
000860*---------------------------------------------------------------* 
000870*    AREA AUXILIAR P/ TESTE DE ( FILE - STATUS )                  
000880*---------------------------------------------------------------* 
000890 77  WRK-ABEND                   PIC S9(04) COMP  VALUE +1111.    
000900 77  WRK-FS-MOVTO                PIC X(02)        VALUE  SPACES.  
000910 77  WRK-FS-MOVOK                PIC X(02)        VALUE  SPACES.  
000920 77  WRK-OPERACAO                PIC X(13) VALUE  SPACES.         
000930 77  WRK-ABERTURA                PIC X(13) VALUE  'NA ABERTURA'.  
000940 77  WRK-LEITURA                 PIC X(13) VALUE  'NA LEITURA'.   
000950 77  WRK-GRAVACAO                PIC X(13) VALUE  'NA GRAVACAO'.  
000960 77  WRK-FECHAMENTO              PIC X(13) VALUE  'NO FECHAMENTO'.
000970                                                                  
000980*---------------------------------------------------------------* 
000990*    AREA AUXILIAR P/ TESTE DE ( FIM-DE-ARQUIVO )                 
001000*---------------------------------------------------------------* 
001010 77  WRK-FIM-MOVTO               PIC X(01) VALUE  'N'.            
001020                                                                  
001030*---------------------------------------------------------------* 
001040*    AREA AUXILIAR P/ NUMERAR CL                                  
001050*---------------------------------------------------------------* 
001060 77  WRK-TAMANHO                 PIC 9(02) VALUE  15.             
001070                                                                  
001080 01  WRK-NUMERO-CL               PIC 9(15) VALUE ZEROS.           
001090 01  FILLER REDEFINES WRK-NUMERO-CL.                              
001100     05  FILLER                  PIC X(05).                       
001110     05  WRK-NUMERO              PIC 9(07).                       
001120     05  WRK-PARCELA             PIC 9(03).                       
001130                                                                  
001140***************************************************************** 
001150 PROCEDURE DIVISION.                                              
001160***************************************************************** 
001170 0000-INICIAR               SECTION.                              
001180                                                                  
001190     OPEN INPUT   MOVTO                                           
001200          OUTPUT  MOVOK.                                          
001210                                                                  
001220     MOVE    WRK-ABERTURA        TO        WRK-OPERACAO.          
001230     PERFORM 1000-TESTAR-FILE-STATUS.                             
001240                                                                  
001250     PERFORM 1300-LER-MOVTO.                                      
001260                                                                  
001270     PERFORM 1400-PROCESSA UNTIL WRK-FIM-MOVTO EQUAL 'S'.         
001280                                                                  
001290     CLOSE  MOVTO                                                 
001300            MOVOK.                                                
001310                                                                  
001320     MOVE    WRK-FECHAMENTO      TO        WRK-OPERACAO.          
001330     PERFORM 1000-TESTAR-FILE-STATUS.                             
001340                                                                  
001350     STOP RUN.                                                    
001360                                                                  
001370*---------------------------------------------------------------* 
001380 0000-99-FIM. EXIT.                                               
001390*---------------------------------------------------------------* 
001400                                                                  
001410*---------------------------------------------------------------* 
001420 1000-TESTAR-FILE-STATUS    SECTION.                              
001430*---------------------------------------------------------------* 
001440                                                                  
001450     PERFORM 1100-TESTAR-FS-MOVTO.                                
001460                                                                  
001470     PERFORM 1200-TESTAR-FS-MOVOK.                                
001480                                                                  
001490*---------------------------------------------------------------* 
001500 1000-99-FIM. EXIT.                                               
001510*---------------------------------------------------------------* 
001520                                                                  
001530*---------------------------------------------------------------* 
001540 1100-TESTAR-FS-MOVTO    SECTION.                                 
001550*---------------------------------------------------------------* 
001560                                                                  
001570     IF WRK-FS-MOVTO    NOT EQUAL '00'                            
001580        DISPLAY '************** PGMNAME **************'           
001590        DISPLAY '*   ERRO ' WRK-OPERACAO  ' DO ARQUIVO   *'       
001600        DISPLAY '*              MOVTO                *'           
001610        DISPLAY '*         FILE STATUS =  ' WRK-FS-MOVTO          
001620                                           '         *'           
001630        DISPLAY '************** PGMNAME **************'           
001640        CALL 'ILBOABN0'      USING         WRK-ABEND.             
001650                                                                  
001660*---------------------------------------------------------------* 
001670 1100-99-FIM. EXIT.                                               
001680*---------------------------------------------------------------* 
001690                                                                  
001700*---------------------------------------------------------------* 
001710 1200-TESTAR-FS-MOVOK    SECTION.                                 
001720*---------------------------------------------------------------* 
001730                                                                  
001740     IF WRK-FS-MOVOK     NOT EQUAL '00'                           
001750        DISPLAY '************** PGMNAME **************'           
001760        DISPLAY '*   ERRO ' WRK-OPERACAO  ' DO ARQUIVO   *'       
001770        DISPLAY '*              MOVOK                *'           
001780        DISPLAY '*         FILE STATUS =  ' WRK-FS-MOVOK          
001790                                           '         *'           
001800        DISPLAY '************** PGMNAME **************'           
001810        CALL 'ILBOABN0'      USING         WRK-ABEND.             
001820                                                                  
001830*---------------------------------------------------------------* 
001840 1200-99-FIM. EXIT.                                               
001850*---------------------------------------------------------------* 
001860                                                                  
001870*---------------------------------------------------------------* 
001880 1300-LER-MOVTO          SECTION.                                 
001890*---------------------------------------------------------------* 
001900                                                                  
001910     READ MOVTO.                                                  
001920                                                                  
001930     IF WRK-FS-MOVTO EQUAL '10'                                   
001940        MOVE  'S'                TO        WRK-FIM-MOVTO          
001950        GO  TO 1300-99-FIM.                                       
001960                                                                  
001970     MOVE WRK-LEITURA            TO        WRK-OPERACAO.          
001980     PERFORM 1100-TESTAR-FS-MOVTO.                                
001990                                                                  
002000*---------------------------------------------------------------* 
002010 1300-99-FIM. EXIT.                                               
002020*---------------------------------------------------------------* 
002030                                                                  
002040*---------------------------------------------------------------* 
002050 1400-PROCESSA              SECTION.                              
002060*---------------------------------------------------------------* 
002070                                                                  
002080     ADD  1                        TO      WRK-NUMERO.            
002090     IF   WRK-NUMERO            EQUAL      ZEROS                  
002100          MOVE 1                   TO      WRK-NUMERO.            
002110     MOVE WRK-NUMERO-CL            TO      MOV-NUMERO.            
002120     MOVE '7'                      TO      MOV-DIGITO.            
002130     CALL 'DIGITO'              USING      MOV-NUMERO             
002140                                           MOV-DIGITO             
002150                                           WRK-TAMANHO.           
002160     PERFORM 1500-GRAVA-MOVOK.                                    
002170                                                                  
002180     PERFORM 1300-LER-MOVTO.                                      
002190                                                                  
002200*---------------------------------------------------------------* 
002210 1400-99-FIM.  EXIT.                                              
002220*---------------------------------------------------------------* 
002230                                                                  
002240*---------------------------------------------------------------* 
002250 1500-GRAVA-MOVOK         SECTION.                                
002260*---------------------------------------------------------------* 
002270                                                                  
002280     WRITE  REG-MOVOK            FROM      REG-MOVG.              
002290                                                                  
002300     MOVE WRK-GRAVACAO           TO        WRK-OPERACAO.          
002310                                                                  
002320     PERFORM 1200-TESTAR-FS-MOVOK.                                
002330                                                                  
002340*---------------------------------------------------------------* 
002350 1500-99-FIM.  EXIT.                                              
002360*---------------------------------------------------------------* 
